Require Import ssreflect ssrbool ssrfun eqtype ssrnat seq div.
Require Import fintype paths finfun ssralg bigops finset prime.
Require Import groups action morphisms group_perm automorphism normal cyclic.

Set Implicit Arguments.
Unset Strict Implicit.
Import Prenex Implicits.

Import GroupScope.

(*****************************************************************************)
(* Standard group notions and constructions based on the prime decomposition *)
(* of the order of the group or its elements.                                *)
(*   pi.-group G      <=> G is a pi-group (<-> pi.-nat #|G|)                 *)
(*     + Recall that here and in the sequel pi can be a prime p              *)
(*   pi.-elt x        <=> x is a pi-element                                  *)
(*                    <-> pi.-nat #[x] (<-> pi.-group <[x]>)                 *)
(*   x.`_pi           == the pi-componet of x: the only pi-element           *)
(*                       y \in <[x]> s.t. x * y^-1 is a pi'-element          *)
(*   pi.-Hall(G) H    <=> H is a Hall pi-subgroup of G                       *)
(*                    <-> [&& H \subset G, pi.-group H & pi^'.-nat #|G : H|] *)
(*                     -> #|H| = #|G|`_pi                                    *)
(*   p.-Sylow(G) P    <=> P is a Sylow p-subgroup of G                       *)
(*                    <-> p.-Hall(G) P                                       *)
(*     + This is only a display notation; note that due to an ugly display   *)
(*       engine bug, Coq will fail to use the notation under a coercion such *)
(*       as is_true, and will display p.-Hall(G) P instead.                  *)
(*   'Syl_p(G)        == the set of the p-Sylow subgroups of G               *)
(*                    := [set P : {group _} | p.-Sylow(G) P]                 *)
(*   p_group P        <=> P is a p-group for some prime p                    *)
(*                    <-> (pdiv #|P|).-group P                               *)
(*   Hall G H         <=> P is a Hall pi-subgroup of G for some pi           *)
(*                    <-> coprime #|H| #|G : H| && (H \subset G)             *)
(*   Sylow G P        <=> P is a Sylow p-subgroup of G for some p            *)
(*                    <-> p_group P && Hall G P                              *)
(*   'O_pi(G)         == the pi-core of G                                    *)
(*                    := the largest normal pi-subgroup of G                 *)
(*   'O_{pi2, pi1}(G) == the pi1,pi2-core of G                               *)
(*                    := the pi1-core of G mod 'O_pi2(G)                     *)
(*     + We have 'O_{pi2, pi1}(G) / 'O_pi2(G) = 'O_pi1(G / 'O_pi2(G))        *)
(*          with 'O_pi2(G) <| 'O_{pi2, pi1}(G) <| G                          *)
(*   'O_{pi3, pi2, pi1}(G) == the pi1, pi2, pi3 -core of G, similarly        *)
(*                         := the pi1-core of G mod 'O_{pi3, pi2}(G)         *)
(*   p.-length_1 G    <=> G has p-length 1, as per B&G p.2                   *)
(*                    <-> 'O_{p^', p, p^'}(G) == G                           *)
(*   'Ohm_n(P)        == if P is a p-group, the group generated by the x of  *)
(*                       order at most p ^ n in P; else, if ~~ p_group P, 1  *)
(*   'Mho^n(P)        == if P is a p-group, the group generated by all the   *)
(*                       x ^+ (p ^ n) for x in P; else, if ~~ p_group P, 1   *)
(*   exponent G       == the exponent of G                                   *)
(*                    := \big[lcmn/1]_(x \in G) #[x]                         *)
(*   abelem E         <=> E is elementary abelian                            *)
(*                    <-> abelian E && (exponent E).-group E                 *)
(*   p.-abelem E      <=> E is an elementary abelian p-group                 *)
(*                    <-> abelem E && p.-group E                             *)
(*                    <-> abelian E && (exponent E %| p) if p is a prime     *)
(*   'E_p(G)          == the set of p-abelem subgroups of G                  *)
(*                    := [set E : {group _} | p.-abelem E && (E \subset G)]  *)
(*   'm_p(G)          == the p-rank of G                                     *)
(*                    := \max_(E \in 'E_p(G)) logn p #|E|                    *)
(*   'm(G)            == the rank of G                                       *)
(*                    := \max_(0 <= p < #|G|.+1) 'm_p(G)                     *)
(*   'E_p^n(G)        == the set of rank n p-abelem subgroups of G           *)
(*                    := [set E \in 'E_p(G) | logn p #|E| == n]              *)
(*                    := [set E \in 'E_p(G) | #|E| == p ^ n]%N if prime p    *)
(*   'E_p^*(G)        == the set of maximal p-abelem subgroups of G          *)
(*                    := [set E | [max E | E \in 'E_p(G)]]                   *)
(*   'E^n(G)          == the set of rank n abelem subgroups of G             *)
(*                    := \bigcup_(0 <= p < #|G|.+1) 'E_p^n(G)                *)
(* NOT YET SUPPORTED/TESTED, MAY BE DEFERRED TO ABELIAN:                     *)
(*   basis A X        <=> X is a basis for the abelian group A               *)
(*                    <-> [/\ <<X>> = A, 1 \notin X                          *)
(*                          & forall x \in X, <[x]> :&: <<X :\ x>> = 1]      *)
(*   abel_type P      == the abelian type of an abelian p-group P            *)
(*                    := the sorted seq of #[x], x in some basis X of P      *)
(*   homocyclic P     <=> P is a homocyclic abelian p-group                  *)
(*                    <-> constant (abel_type P)                             *)
(* Note that notions are always defined on sets even though their name       *)
(* indicates "group" properties; the actual definition of the notion never   *)
(* tests for the group property, since this property will always be          *)
(* provided by a (canonical) group structure. Similarly, p-group properties  *)
(* assume without test that p is a prime.                                    *)
(*****************************************************************************)

Section PgroupDefs.

(* We defer the definition of the functors ('0_p(G), etc) because they need *)
(* to quantify over the finGroupType explicitly.                            *)

Variable gT : finGroupType.
Implicit Type x : gT.
Implicit Types A B : {set gT}.
Implicit Type pi : nat_pred.
Implicit Types p n : nat.

Definition pgroup pi A := pi.-nat #|A|.

Definition p_group A := pgroup (pdiv #|A|) A.

Definition p_elt pi x := pi.-nat #[x].

Definition compg x pi := x ^+ (chinese #[x]`_pi #[x]`_pi^' 1 0).

Definition Hall A B := (B \subset A) && coprime #|B| #|A : B|.

Definition pHall pi A B := [&& B \subset A, pgroup pi B & pi^'.-nat #|A : B|].

Definition Syl p A := [set P : {group gT} | pHall p A P].

Definition Sylow A B := p_group B && Hall A B.

Definition exponent A := \big[lcmn/1%N]_(x \in A) #[x].

Definition abelem A := abelian A && pgroup (exponent A) A.

Definition p_abelem p A := abelem A && pgroup p A.

Definition pElem p A := [set E : {group gT} | (E \subset A) && p_abelem p E].

Definition pnElem p n A := [set E \in pElem p A | logn p #|E| == n].

Definition nElem n A :=  \bigcup_(0 <= p < #|A|.+1) pnElem p n A.

Definition pmaxElem p A := [set E | [max E | (E \subset A) && p_abelem p E]].

Definition p_rank p A := \max_(E \in pElem p A) logn p #|E|.

Definition rank A := \max_(0 <= p < #|A|.+1) p_rank p A.

Definition basis A X :=
  [&& <<X>> == A, 1 \notin X
    & forallb x, (x \in X) ==> trivg (<[x]> :&: <<X :\ x>>)].

Definition abel_type A :=
  if pick (basis A) is Some X then
    sort leq (maps order (enum (mem X)))
  else [::].

Definition homocyclic A := [&& abelian A, p_group A & constant (abel_type A)].

End PgroupDefs.

Prenex Implicits p_group Hall Sylow exponent abelem rank.
Prenex Implicits basis abel_type homocyclic.

Notation "pi .-group" := (pgroup pi)
  (at level 2, format "pi .-group") : group_scope.

Notation "pi .-elt" := (p_elt pi)
  (at level 2, format "pi .-elt") : group_scope.

Notation "x .`_ pi" := (compg x pi)
  (at level 3, format "x .`_ pi") : group_scope.

Notation "pi .-Hall ( G )" := (pHall pi G)
  (at level 8, format "pi .-Hall ( G )") : group_scope.

Notation "p .-Sylow ( G )" := (nat_pred_of_nat p).-Hall(G)
  (at level 8, format "p .-Sylow ( G )") : group_scope.

Notation "''Syl_' p ( G )" := (Syl p G)
  (at level 8, p at level 2, format "''Syl_' p ( G )") : group_scope.

Notation "p .-abelem" := (p_abelem p)
  (at level 2, format "p .-abelem") : group_scope.

Notation "''E_' p ( G )" := (pElem p G)
  (at level 8, p at level 2, format "''E_' p ( G )") : group_scope.

Notation "''E_' p ^ n ( G )" := (pnElem p n G)
  (at level 8, p, n at level 2, format "''E_' p ^ n ( G )") : group_scope.

Notation "''E' ^ n ( G )" := (nElem n G)
  (at level 8, n at level 2, format "''E' ^ n ( G )") : group_scope.

Notation "''E_' p ^* ( G )" := (pmaxElem p G)
  (at level 8, p at level 2, format "''E_' p ^* ( G )") : group_scope.

Section PgroupFunctor.

Implicit Type gT rT : finGroupType.

Variable F : nat -> forall gT, {set gT} -> {set gT}.
Hypothesis sF : forall p gT (G : {group gT}), F p G \subset G.
Hypothesis funF : forall p gT rT (G : {group gT}) (f : {morphism G >-> rT}),
  f @* (F p G) \subset F p (f @* G).

Definition pgroup_functor gT (A : {set gT}) :=
  <<if p_group A then F (pdiv #|A|) A else 1>>.

Lemma pgroup_functor_1 : forall gT (G : {group gT}),
  ~~ p_group G -> pgroup_functor G = 1.
Proof. by move=> gT G npG; rewrite /pgroup_functor -if_neg npG genGid. Qed.

Lemma pgroup_functor_sub : forall gT (G : {group gT}),
  pgroup_functor G \subset G.
Proof. by move=> gT G; rewrite gen_subG; case: ifP; rewrite ?sub1G. Qed.

Lemma pgroup_functor1 : forall gT, pgroup_functor 1 = 1 :> {set gT}.
Proof. move=> gT; apply/trivgP; exact: pgroup_functor_sub. Qed.

Lemma pgroup_functor_p : forall (p : nat) gT (G : {group gT}),
  p.-group G -> pgroup_functor G = <<F p G>>.
Proof.
move=> p gT G pG; case: (leqP #|G| 1).
  rewrite -trivg_card; move/trivGP=> ->; apply/eqP.
  by rewrite pgroup_functor1 eqset_sub sub1G gen_subG sF.
move/prime_pdiv=> pr_p.
have def_p: pdiv #|G| = p by apply/eqP; exact: (pnatP _ _ pG) (dvdn_pdiv _).
by rewrite /pgroup_functor /p_group def_p pG.
Qed.

Lemma morphim_pgroup_functor : forall gT rT
    (G : {group gT}) (f : {morphism G >-> rT}),
  f @* (pgroup_functor G) \subset pgroup_functor (f @* G).
Proof.
move=> gT rT G f; case pG: (p_group G); last first.
  by rewrite pgroup_functor_1 ?pG // morphim1 sub1G.
rewrite {1}/pgroup_functor pG morphim_gen //.
rewrite (@pgroup_functor_p (pdiv #|G|)) ?genS ?funF //.
by apply: pnat_dvd pG; exact: dvdn_morphim.
Qed.

End PgroupFunctor.

Section Pcore.

(* A functor needs to quantify over the finGroupType just beore the set. *)

Variables (n p : nat) (pi : nat_pred) (gT : finGroupType) (A : {set gT}).

Definition pcore := << \bigcup_(G : {group gT} | pi.-group G && (G <| A)) G >>.

Definition OhmFun := [set x \in A | x ^+ (p ^ n) == 1].

Definition MhoFun := (fun x => x ^+ (p ^ n)) @: A.

Canonical Structure pcore_group := Eval hnf in [group of pcore].

End Pcore.

Notation "''O_' pi ( G )" := (pcore pi G)
  (at level 8, pi at level 2, format "''O_' pi ( G )") : group_scope.
Notation "''O_' pi ( G )" := (pcore_group pi G) : subgroup_scope.

Definition Ohm n := pgroup_functor (OhmFun n).
Canonical Structure Ohm_group n gT A := Eval hnf in [group of @Ohm n gT A].
Notation "''Ohm_' n ( G )" := (Ohm n G)
  (at level 8, n at level 2, format "''Ohm_' n ( G )") : group_scope.
Notation "''Ohm_' n ( G )" := (Ohm_group n G) : subgroup_scope.

Definition Mho n := pgroup_functor (MhoFun n).
Canonical Structure Mho_group n gT A := Eval hnf in [group of @Mho n gT A].
Notation "''Mho^' n ( G )" := (Mho n G)
  (at level 8, n at level 2, format "''Mho^' n ( G )") : group_scope.
Notation "''Mho^' n ( G )" := (Mho_group n G) : subgroup_scope.

Section PcoreMod.

Variables (gT : finGroupType) (A : {set gT}) (pi : nat_pred) (B : {set gT}).

Definition pcore_mod := coset_of A @*^-1 'O_pi(B / A).
Canonical Structure pcore_mod_group := Eval hnf in [group of pcore_mod].

End PcoreMod.

(* Coq's n-ary notation facility is too brain-dead to be used here. *)
(* We need to repeat A in the notation to allow the group sructure  *)
(* to be inferred; a canonical functor structure might fix this.    *)
Notation "''O_{' p1 , p2 } ( A )" := (pcore_mod 'O_p1(A) p2 A)
  (at level 8, format "''O_{' p1 ,  p2 } ( A )") : group_scope.
Notation "''O_{' p1 , p2 , p3 } ( A )" := (pcore_mod 'O_{p1, p2}(A) p3 A)
  (at level 8, format "''O_{' p1 ,  p2 ,  p3 } ( A )") : group_scope.
Notation "''O_{' p1 , p2 } ( A )" :=
  (pcore_mod_group 'O_p1(A) p2 A) : subgroup_scope.
Notation "''O_{' p1 , p2 , p3 } ( A )" :=
  (pcore_mod_group 'O_{p1, p2}(A) p3 A) : subgroup_scope.

Definition plength_1 (gT : finGroupType) p (A : {set gT}) :=
  'O_{p^', p, p^'}(A) == A.

Notation "p .-length_1" := (plength_1 p)
  (at level 2, format "p .-length_1") : group_scope.

Section PgroupProps.

Variable gT : finGroupType.
Implicit Type pi : nat_pred.
Implicit Type p : nat.
Implicit Types x y z : gT.
Implicit Types A B C D : {set gT}.
Implicit Types G H K P Q : {group gT}.

Lemma pgroupP : forall pi G,
  reflect (forall p, prime p -> p %| #|G| -> p \in pi) (pi.-group G).
Proof. move=> pi G; exact: pnatP. Qed.

Lemma pgroup1 : forall pi, pi.-group (1 : {set gT}).
Proof. by move=> pi; rewrite /pgroup cards1. Qed.

Lemma pgroupS : forall pi G H, H \subset G -> pi.-group G -> pi.-group H.
Proof. move=> pi G H sHG; exact: pnat_dvd (group_dvdn sHG). Qed.

Lemma pgroupM : forall pi G H, pi.-group (G * H) = pi.-group G && pi.-group H.
Proof.
move=> pi G H; have GH_pos: 0 < #|G :&: H| := ltn_0group _.
rewrite /pgroup -(divn_mull #|_| GH_pos) card_mulG -(LaGrangeI G H) -mulnA.
by rewrite divn_mulr // -(LaGrangeI H G) setIC !pnat_mul andbCA; case pnat.
Qed.

Lemma pgroup_p : forall p P, p.-group P -> p_group P.
Proof.
move=> p P pgP; case: (leqP #|P| 1).
  rewrite -trivg_card; move/trivgP->; exact: pgroup1.
move/prime_pdiv=> pr_q; have:= pgroupP _ _ pgP _ pr_q (dvdn_pdiv _).
by rewrite /p_group; move/eqnP->.
Qed.

Lemma p_groupP : forall P, p_group P -> exists2 p, prime p & p.-group P.
Proof.
move=> P; case: (ltnP 1 #|P|); first by move/prime_pdiv; exists (pdiv #|P|).
rewrite -trivg_card; move/trivgP=> -> _; exists 2 => //; exact: pgroup1.
Qed.

Lemma card_Hall : forall pi G H, pi.-Hall(G) H -> #|H| = #|G|`_pi.
Proof.
move=> pi G H; case/and3P=> sHG piH pi'H.
by rewrite -(LaGrange sHG) partn_mul ?LaGrange // part_pnat ?part_p'nat ?muln1.
Qed.

Lemma pHall_subset : forall pi A B, pi.-Hall(A) B -> B \subset A.
Proof. by move=> pi A B; case/andP. Qed.

Lemma pHall_pgroup : forall pi A B, pi.-Hall(A) B -> pi.-group B.
Proof. by move=> pi A B; case/and3P. Qed.

Lemma pHallP : forall pi G H,
  reflect (H \subset G /\ #|H| = #|G|`_pi) (pi.-Hall(G) H). 
Proof.
move=> pi G H; apply: (iffP idP) => [piH | [sHG oH]].
  split; [exact: pHall_subset piH | exact: card_Hall].
rewrite /pHall sHG -group_divn // /pgroup oH.
by rewrite -{2}(@partnC pi #|G|) ?divn_mulr ?pnat_part.
Qed.

Lemma pHallE : forall pi G H,
  pi.-Hall(G) H = (H \subset G) && (#|H| == #|G|`_pi).
Proof. by move=> pi G H; apply/pHallP/andP=> [] [->]; move/eqP. Qed.

Lemma pHall_Hall : forall pi A B, pi.-Hall(A) B -> Hall A B.
Proof.
move=> pi A B; case/and3P=> sBA piB pi'B.
by rewrite /Hall sBA (pnat_coprime piB).
Qed.

Lemma Hall_pi : forall G H, Hall G H -> \pi(#|H|).-Hall(G) H.
Proof.
move=> G H; case/andP=> sHG coHG.
by rewrite /pHall sHG /pgroup pnat_pi -?coprime_pi'.
Qed.

Lemma HallP : forall G H, Hall G H -> exists pi, pi.-Hall(G) H.
Proof. move=> G H HallH; exists \pi(#|H|); exact: Hall_pi. Qed.

Lemma p_Sylow : forall p G P, p.-Sylow(G) P -> Sylow G P.
Proof.
move=> p G P pP.
by rewrite /Sylow (pgroup_p (pHall_pgroup pP)) (pHall_Hall pP).
Qed.

Lemma Hall1 : forall G, Hall G 1.
Proof. by move=> G; rewrite /Hall sub1G cards1 coprime1n. Qed.

Lemma p_group1 : @p_group gT 1.
Proof. by rewrite (@pgroup_p 2) ?pgroup1. Qed.

Lemma Sylow1 : forall G, Sylow G 1.
Proof. by move=> G; rewrite /Sylow p_group1 Hall1. Qed.

Lemma eq_pHall : forall pi1 pi2 A B,
  pi1 =i pi2 -> pi1.-Hall(A) B = pi2.-Hall(A) B.
Proof.
move=> pi1 pi2 A B eq_pi; congr [&& _, _ & _]; apply: eq_pnat => // q.
congr (~~ _); exact: eq_pi.
Qed.

Lemma SylowP : forall G P,
  reflect (exists2 p, prime p & p.-Sylow(G) P) (Sylow G P).
Proof.
move=> G P; apply: (iffP idP) => [| [p _]]; last exact: p_Sylow.
case/andP; case/p_groupP=> p p_pr; case/p_natP=> // [[P1 _ | n oP]].
  have{p p_pr P1} ->: P = 1%G by apply/trivGP; rewrite trivg_card P1.
  pose p := pdiv #|G|.+1; have pr_p: prime p by rewrite prime_pdiv ?ltnS.
  exists p; rewrite // pHallE sub1G cards1 part_p'nat //.
  apply/pgroupP=> q pr_q qG; apply/eqnP=> def_q.
  have: p %| #|G| + 1 by rewrite addn1 dvdn_pdiv.
  by rewrite dvdn_addr -def_q // euclid1.
move/Hall_pi; rewrite oP pi_of_exp // (eq_pHall _ _ (pi_of_prime _)) //.
by exists p.
Qed.

Lemma p_elt_exp : forall pi x m, pi.-elt (x ^+ m) = (#[x]`_pi^' %| m).
Proof.
move=> pi x m; apply/idP/idP=> [pi_xm|].
  rewrite -(@gauss _ #[x ^+ m]); last first.
    by rewrite coprime_sym (pnat_coprime pi_xm) ?pnat_part.
  apply: (@dvdn_trans #[x]); first by rewrite -{2}[#[x]](partnC pi) ?dvdn_mull.
  by rewrite order_dvd mulnC expgn_mul order_expn1.
case/dvdnP=> q ->{m}; rewrite mulnC; apply: pnat_dvd (pnat_part pi #[x]).
by rewrite order_dvd -expgn_mul mulnC mulnA partnC // -order_dvd dvdn_mulr.
Qed.

Lemma mem_p_elt : forall pi x G, pi.-group G -> x \in G -> pi.-elt x.
Proof. by move=>pi x G piG Gx; apply: pgroupS piG; rewrite cycle_h. Qed.

Lemma p_eltM_norm : forall pi x y, x \in 'N(<[y]>) ->
  pi.-elt x -> pi.-elt y -> pi.-elt (x * y).
Proof.
move=> pi x y nyx pi_x pi_y; apply: (@mem_p_elt pi _ (<[x]> <*> <[y]>)%G).
  rewrite /= norm_mulgenE ?cycle_h // pgroupM; exact/andP.
by rewrite groupM // mem_gen // inE cyclenn ?orbT.
Qed.

Lemma p_eltM :  forall pi x y, commute x y ->
  pi.-elt x -> pi.-elt y -> pi.-elt (x * y).
Proof.
move=> pi x y cxy; apply: p_eltM_norm; apply: (subsetP (cent_subset _)).
rewrite cent_gen cent_set1; exact/cent1P.
Qed.

Lemma p_elt1 : forall pi, pi.-elt (1 : gT).
Proof. by move=> pi; rewrite /p_elt order1. Qed.

Lemma p_eltV : forall pi x, pi.-elt x^-1 = pi.-elt x.
Proof. by move=> pi x; rewrite /p_elt orderV. Qed.

Lemma p_eltX : forall pi x n, pi.-elt x -> pi.-elt (x ^+ n).
Proof.
by move=> pi x n; rewrite -{1}[x]expg1 !p_elt_exp dvdn1; move/eqnP->.
Qed.

Lemma p_eltJ : forall pi x y, pi.-elt (x ^ y) = pi.-elt x.
Proof. by move=> pi x y; congr pnat; rewrite orderJ. Qed.

Lemma sub_p_elt : forall pi1 pi2 x,
  {subset pi1 <= pi2} -> pi1.-elt x -> pi2.-elt x.
Proof. move=> pi1 pi2 x pi12; apply: subd_pnat => q _; exact: pi12. Qed.

Lemma eq_p_elt : forall pi1 pi2 x, pi1 =i pi2 -> pi1.-elt x = pi2.-elt x.
Proof. move=> pi1 pi2 x pi12; exact: eq_pnat. Qed.

Lemma p_eltNK : forall pi x, pi^'^'.-elt x = pi.-elt x.
Proof. move=> pi x; exact: pnatNK. Qed.

Lemma eq_expg_mod_order : forall x m n,
  (x ^+ m == x ^+ n) = (m %% #[x] == n %% #[x]).
Proof.
move=> x m n; wlog le_nm: m n / n <= m.
  by move=> IH; case/orP: (leq_total m n); move/IH; rewrite // eq_sym => ->.
rewrite eqn_mod_dvd // -{1}(subnK le_nm) expgn_add.
by rewrite (can2_eq (mulKg _) (mulKVg _)) mulVg order_dvd.
Qed.

Lemma eq_compg : forall pi1 pi2 x, pi1 =i pi2 -> x.`_pi1 = x.`_pi2.
Proof.
move=> pi1 pi2 x pi12; congr (x ^+ (chinese _ _ 1 0)); apply: eq_partn => //.
move=> q; congr (~~ _); exact: pi12.
Qed.

Lemma compgNK : forall pi x, x.`_pi^'^' = x.`_pi.
Proof. by move=> pi x; rewrite /compg !partnNK. Qed.

Lemma cycle_comp : forall pi (x : gT), x.`_pi \in <[x]>.
Proof. move=> pi x; exact: cycle_in. Qed.

Lemma compVg : forall pi x, (x^-1).`_pi = (x.`_pi)^-1.
Proof. by move=> pi x; rewrite /compg expVgn orderV. Qed.

Lemma comp1g : forall pi, 1.`_pi = 1 :> gT.
Proof. move=> pi; exact: exp1gn. Qed.

Lemma compJg : forall pi x y, (x ^ y).`_pi = x.`_pi ^ y.
Proof. by move=> pi x y; rewrite /compg orderJ conjXg. Qed.

Lemma p_elt_comp : forall pi x, pi.-elt x.`_pi.
Proof. by move=> pi x; rewrite p_elt_exp /chinese addn0 mul1n dvdn_mulr. Qed.

Lemma compgC : forall pi x, x.`_pi * x.`_pi^' = x.
Proof.
move=> pi x; apply/eqP; rewrite -{3}[x]expg1 -expgn_add eq_expg_mod_order.
rewrite partnNK -{5 6}(@partnC pi #[x]) // /chinese !addn0.
by rewrite chinese_remainder ?chinese_modl ?chinese_modr ?coprime_partC ?eqxx.
Qed.

Lemma p'_elt_comp : forall pi x, pi^'.-elt (x * (x.`_pi)^-1).
Proof.
by move=> pi x; rewrite -{1}(compgC pi^' x) compgNK mulgK p_elt_comp.
Qed.

Lemma order_comp : forall pi (x : gT), #[x.`_pi] = #[x]`_pi.
Proof.
move=> pi x; rewrite -{2}(compgC pi x) order_mul; [|exact: commuteX2|].
  rewrite partn_mul // part_pnat ?part_p'nat ?muln1 //; exact: p_elt_comp.
apply: (@pnat_coprime pi); exact: p_elt_comp.
Qed.

Lemma compMg : forall pi x y, commute x y -> (x * y).`_pi = x.`_pi * y.`_pi.
Proof.
move=> pi x y cxy; pose m := #|<<[set x; y]>>|.
pose k := chinese m`_pi m`_pi^' 1 0; have m_pos: 0 < m := ltn_0group _.
suffices kXpi: forall z, z \in <<[set x; y]>> -> z.`_pi = z ^+ k.
  by rewrite !kXpi ?expMgn // ?groupM ?mem_gen // (set21, set22).
move=> z xyz; have{xyz} zm: #[z] %| m by rewrite group_dvdn ?cycle_h.
apply/eqP; rewrite eq_expg_mod_order -{3 4}[#[z]](partnC pi) //.
rewrite chinese_remainder ?chinese_modl ?chinese_modr ?coprime_partC //.
rewrite -!(modn_dvdm k (partn_dvd _ m_pos zm)).
rewrite chinese_modl ?chinese_modr ?coprime_partC //.
by rewrite !modn_dvdm ?partn_dvd ?eqxx.
Qed.

Lemma compXg : forall pi x n, (x ^+ n).`_pi = x.`_pi ^+ n.
Proof.
move=> pi x; elim=> [|n IHn]; first exact: comp1g.
rewrite !expgS compMg ?IHn //; exact: commuteX.
Qed.

Lemma compg1P: forall pi x, reflect (x.`_pi = 1) (pi^'.-elt x).
Proof.
move=> pi x; rewrite -{2}[x]expg1 p_elt_exp -order_comp compgNK.
rewrite order_dvd expg1; exact: eqP.
Qed.

Lemma compg_p_elt : forall pi x, pi.-elt x -> x.`_pi = x.
Proof.
move=> pi x; rewrite -p_eltNK -{3}(compgC pi x); move/compg1P->.
by rewrite mulg1.
Qed.

Lemma subd_compg : forall pi1 pi2 x,
  {in \pi(#[x]), {subset pi1 <= pi2}} -> x.`_pi2.`_pi1 = x.`_pi1.
Proof.
move=> pi1 pi2 x pi12; rewrite -{2}(compgC pi2 x).
rewrite compMg; last exact: commuteX2.
rewrite (compg1P _ x.`_pi2^' _) ?mulg1 //.
apply: subd_pnat (p_elt_comp _ x) => p; rewrite order_comp => pi_p.
apply: contra; apply: pi12.
by rewrite -[#[x]](partnC pi2^') // primes_mul // pi_p.
Qed.

Lemma prod_compg : forall x, \prod_(0 <= p < #[x].+1) x.`_p = x.
Proof.
move=> x; pose lp n := [pred p | p < n].
have: (lp #[x].+1).-elt x by apply/pnatP=> // p _; exact: dvdn_leq.
move/compg_p_elt=> def_x; symmetry; rewrite -{1}def_x {def_x}.
elim: _.+1 => [|p IHp].
  by rewrite big_seq0; apply/compg1P; exact/pgroupP.
rewrite big_nat_recr /= -{}IHp -(compgC (lp p) x.`__); congr (_ * _).
  rewrite subd_compg // => q _; exact: leqW.
set y := _.`__; rewrite -(compgC p y) (compg1P p^' _ _) ?mulg1.
  by rewrite 2?subd_compg // => q _; move/eqnP->; rewrite inE //= inE /= ltnn.
rewrite /p_elt pnatNK !order_comp -partnI.
apply: subd_pnat (pnat_part _ _) => q _; do 3!rewrite !inE /=.
by rewrite ltnS -leqNgt -eqn_leq.
Qed.

End PgroupProps.

Section ModP.

Variable (gT : finGroupType) (sT : finType).

Variable to : {action gT &-> sT}.

(***********************************************************************)
(*                                                                     *)
(*           The mod p lemma                                           *)
(*                                                                     *)
(***********************************************************************)

Lemma pgroup_fix_mod : forall p (G : {group gT}) (S : {set sT}),
  prime p -> p.-group G -> [acts (G | to) on S] ->
   #|S| %% p = #|'C_S(G | to)| %% p.
Proof.
move=> p G S prime_p; case/p_natP => // n cardG GactS; apply/eqP.
rewrite -(cardsID 'C(G | to)) eqn_mod_dvd (leq_addr, addKn) //.
set S1 := S :\: _; have: [acts (G | to) on S1].
  apply/actsP=> a Ga x; rewrite !in_setD (actsP GactS) //; congr (~~ _ && _).
  by apply: actsP Ga x; rewrite norm_act_fix ?normG.
move/acts_sum_card_orbit <-.
apply big_prop => // [m1 m2|X]; first exact: dvdn_add.
case/imsetP=> x; case/setDP=> _ nfx ->{X}.
have:= dvdn_orbit to G x; rewrite cardG.
case/dvdn_pfactor=> [//|[_|m _ ->]]; last exact: dvdn_mulr.
move/card_orbit1=> fix_x; case/afixP: nfx => a Ga; apply/set1P.
by rewrite -fix_x mem_imset.
Qed.

End ModP.

Section Morphim.

Variables (aT rT : finGroupType) (D : {group aT}) (f : {morphism D >-> rT}).
Implicit Type pi : nat_pred.
Implicit Types G H P : {group aT}.

Lemma morphim_pgroup : forall pi G, pi.-group G -> pi.-group (f @* G).
Proof. move=> pi G; apply: pnat_dvd; exact: dvdn_morphim. Qed.

Lemma morphim_p_index : forall pi G H,
  H \subset D -> pi.-nat #|G : H| -> pi.-nat #|f @* G : f @* H|.
Proof.
move=> pi G H dH; apply: pnat_dvd; apply: dvdn_morphim_index.
by rewrite subIset // orbC dH.
Qed.

Lemma morphim_pHall : forall pi G H,
  H \subset D -> pi.-Hall(G) H -> pi.-Hall(f @* G) (f @* H).
Proof.
move=> pi G H dH; case/and3P=> sHG piH pi'GH.
by rewrite /pHall morphimS // morphim_pgroup // morphim_p_index.
Qed.

Lemma morphim_Hall : forall G H,
   H \subset D -> Hall G H -> Hall (f @* G) (f @* H).
Proof.
move=> G H dH; case/HallP=> pi piH; apply: (@pHall_Hall _ pi).
exact: morphim_pHall.
Qed.

Lemma morphim_pSylow : forall p G P,
  P \subset D -> p.-Sylow(G) P -> p.-Sylow(f @* G) (f @* P).
Proof. move=> p; exact: morphim_pHall. Qed.

Lemma morphim_p_group : forall P, p_group P -> p_group (f @* P).
Proof. move=> P; move/morphim_pgroup; exact: pgroup_p. Qed.

Lemma morphim_Sylow : forall G P,
  P \subset D -> Sylow G P -> Sylow (f @* G) (f @* P).
Proof.
move=> G P dP; case/andP=> pP hallP.
by rewrite /Sylow morphim_p_group // morphim_Hall.
Qed.

Lemma morph_p_elt : forall pi x, x \in D -> pi.-elt x -> pi.-elt (f x).
Proof. move=> pi x Dx; apply: pnat_dvd; exact: morph_order. Qed.

Lemma morph_comp : forall pi x, x \in D -> f x.`_pi = (f x).`_pi.
Proof.
move=> pi x Dx; rewrite -{2}(compgC pi x) morphM ?groupX //.
rewrite compMg; last by rewrite !morphX //; exact: commuteX2.
have: pi.-elt (f x.`_pi) by rewrite morph_p_elt ?groupX ?p_elt_comp //.
have: pi^'.-elt (f x.`_pi^') by rewrite morph_p_elt ?groupX ?p_elt_comp //.
by move/compg1P->; move/compg_p_elt->; rewrite mulg1.
Qed.

End Morphim.

Section PiCoreProps.

Variable gT : finGroupType.
Implicit Type A : {set gT}.
Implicit Types G H : {group gT}.

Lemma pcore_max : forall pi G, pi.-group 'O_pi(G) && ('O_pi(G) <| G).
Proof.
move=> pi G; rewrite /'O_pi(G).
apply big_prop => [| A B | H]; last by rewrite genGid.
  by rewrite /(_ <| _) gen0 pgroup1 /pgroup normaliser1 sub1G subsetT.
case/and3P=> piA sAG nAG; case/and3P=> piB sBG nBG.
rewrite -mulgenE -mulgen_idl -mulgen_idr.
apply/and3P; split; last 2 [by rewrite gen_subG subUset sAG].
  have gen_pos: 0 < #|<<_ : {set gT}>>| := ltn_0group _.
  rewrite /pgroup norm_mulgenE ?(subset_trans sAG) //.
  have: pi.-nat (#|<<A>>| * #|<<B>>|)%N by rewrite pnat_mul //; exact/andP.
  by rewrite -card_mulG pnat_mul //; case/andP.
by apply: subset_trans (norm_mulgen _ _); rewrite subsetI nAG.
Qed.

Lemma pgroup_pcore : forall pi G, pi.-group 'O_pi(G).
Proof. by move=> pi G; case/andP: (pcore_max pi G). Qed.

Lemma pcore_normal : forall pi G, 'O_pi(G) <| G.
Proof. by move=> pi G; case/andP: (pcore_max pi G). Qed.

Lemma subset_pcore : forall pi G H,
  pi.-group H -> H <| G -> H \subset 'O_pi(G).
Proof.
move=> pi G H piH nHG; apply/subsetP=> x Hx; apply: mem_gen; apply/bigcupP.
by exists H; rewrite // piH.
Qed.

Lemma char_pcore : forall pi G, 'O_pi(G) \char G.
Proof.
move=> pi G.
have sOG: 'O_pi(G) \subset G by case/andP: (pcore_normal pi G).
apply/charP; split=> //= f injf fG; apply/morphim_fixP=> //.
apply: subset_pcore; first by rewrite morphim_pgroup ?pgroup_pcore.
by rewrite -{3}fG  morphim_normal ?pcore_normal.
Qed.

Lemma Cauchy : forall p G, prime p -> p %| #|G| -> {x | x \in G & #[x] = p}.
Proof.
move=> p G pr_p; elim: {G}_.+1 {-2}G (ltnSn #|G|) => // n IHn G; rewrite ltnS.
pose xpG := [pred x \in G | #[x] == p] => leGn pG.
case: (pickP xpG) => [x|no_x]; first by case/andP=> Gx; move/eqP; exists x.
have{pG n leGn IHn} pZ: p %| #|'C_G(G)|.
  have:= pG; rewrite -(cardsID 'C(G)) dvdn_addl //.
  have: [acts (G | 'J) on G :\: 'C(G)]; last move/acts_sum_card_orbit <-.
    by apply/actsP=> x Gx y; rewrite !inE -!mem_conjgV -centJ conjGid ?groupV.
  apply big_prop => // [|C]; first exact: dvdn_add.
  case/imsetP=> x; case/setDP=> Gx nCx ->{C}; rewrite card_orbit conjg_astab1.
  move: pG; rewrite -(LaGrange (subsetIl G 'C[x]%G)) euclid //; case/orP => //.
  case/IHn=> [|y]; last first.
    by case/setIP=> Gy _; move/eqP=> oyp; case/andP: (no_x y).
  apply: leq_trans leGn; apply: proper_card; rewrite properE subsetIl.
  by rewrite subsetI subset_refl /= -cent_set1 centsC sub1set.
suff: predC1 p p by case/eqP.
have: p^'.-group 'O_p^'(G) by exact: pgroup_pcore.
move/pgroupP; apply=> //; apply: dvdn_trans pZ (group_dvdn _).
apply/subsetP=> x; case/setIP=> Cx Gx.
rewrite -sub1set -gen_subG subset_pcore //; last first.
  rewrite /(_ <| _) cycle_h // (subset_trans _ (norm_gen _)) //.
  by rewrite -cent_set1 centsC sub1set.
apply/pgroupP=> q _ p_x; apply/eqP=> def_q; rewrite {q}def_q in p_x.
case/dvdnP: p_x => m ox; case/idP: (no_x (x ^+ m)) => /=.
have m_pos: 0 < m by rewrite -(ltn_pmul2r (ltn_0prime pr_p)) -ox ltn_0order.
by rewrite /= groupX //= order_gcd // [#[x]]ox gcdnC gcdn_mulr divn_mulr.
Qed.

End PiCoreProps.

Section OhmProps.

Section bases.

Variables (n p : nat) (gT rT : finGroupType).
Variables (G : {group gT}) (f : {morphism G >-> rT}).

Lemma OhmFun_sub : OhmFun n p G \subset G.
Proof. by apply/subsetP=> x; rewrite inE; case/andP. Qed.

Lemma MhoFun_sub : MhoFun n p G \subset G.
Proof. apply/subsetP=> y; case/imsetP=> x Gx ->; exact: groupX. Qed.

Lemma morphim_OhmFun :
  f @* (OhmFun n p G) \subset OhmFun n p (f @* G).
Proof.
apply/subsetP=> y; case/morphimP=> x _; rewrite inE; case/andP=> Gx oxpn ->.
by rewrite inE morphimEdom mem_imset //= -morphX // (eqP oxpn) morph1.
Qed.

Lemma morphim_MhoFun : f @* (MhoFun n p G) = MhoFun n p (f @* G).
Proof.
rewrite morphimEdom morphimEsub ?MhoFun_sub // /MhoFun -!imset_comp.
by apply: dfequal_imset => x Gx; rewrite /= morphX.
Qed.

End bases.

Section generic.

Variables (n : nat) (gT : finGroupType).
Implicit Type rT : finGroupType.
Implicit Type G : {group gT}.

Hint Resolve OhmFun_sub MhoFun_sub.

Lemma Ohm_subset : forall G, 'Ohm_n(G) \subset G.
Proof. exact: pgroup_functor_sub. Qed.

Lemma Mho_subset : forall G, 'Mho^n(G) \subset G.
Proof. exact: pgroup_functor_sub. Qed.

Hint Resolve Ohm_subset Mho_subset.

Lemma morphim_Ohm : forall rT G (f : {morphism G >-> rT}),
  f @* 'Ohm_n(G) \subset 'Ohm_n(f @* G).
Proof. by apply: morphim_pgroup_functor => //; exact: morphim_OhmFun. Qed.

Lemma morphim_Mho : forall rT G (f : {morphism G >-> rT}),
  f @* 'Mho^n(G) \subset 'Mho^n(f @* G).
Proof.
by apply: morphim_pgroup_functor => // p aT arT G f; rewrite morphim_MhoFun.
Qed.

Lemma OhmE : forall (p : nat) G, p.-group G -> 'Ohm_n(G) = <<OhmFun n p G>>.
Proof. move=> p; exact: pgroup_functor_p. Qed.

Lemma MhoE : forall (p : nat) G, p.-group G -> 'Mho^n(G) = <<MhoFun n p G>>.
Proof. move=> p; exact: pgroup_functor_p. Qed.

Lemma morphim_Mho_eq : forall rT G (f : {morphism G >-> rT}),
  p_group G -> f @* 'Mho^n(G) = 'Mho^n(f @* G).
Proof.
move=> rT G f; case/p_groupP=> p _ pG; rewrite (MhoE pG) morphim_gen //.
by rewrite morphim_MhoFun -pgroup_functor_p // morphim_pgroup.
Qed.

(* Francois, this is the generic proof -- GG *)
Lemma char_Ohm : forall G, 'Ohm_n(G) \char G.
Proof.
move=> G; apply/charP; split=> //= f injf defG; apply/eqP.
rewrite eqset_sub_card /= -{4}defG morphim_Ohm card_morphim ker_injm //=.
by rewrite group_index1 /= (setIidPr _).
Qed.

Lemma char_Mho : forall G, 'Mho^n(G) \char G.
Proof.
move=> G; apply/charP; split=> //= f injf defG; apply/eqP.
rewrite eqset_sub_card /= -{4}defG morphim_Mho card_morphim ker_injm //=.
by rewrite group_index1 /= (setIidPr _).
Qed.

End generic.

Variable gT : finGroupType.
Implicit Type A : {set gT}.
Implicit Type G H E : {group gT}.
Implicit Type pi : nat_pred.

Lemma dvdn_exponent : forall x G, x \in G -> #[x] %| exponent G.
Proof. by move=> x G Gx; rewrite /exponent (bigD1 x) //= dvdn_lcml. Qed.

Lemma exponentP : forall G n,
  reflect (forall x, x \in G -> x ^+ n = 1) (exponent G %| n).
Proof.
rewrite /exponent => G n; apply: (iffP idP) => [eGn x Gx | eGn].
  apply/eqP; rewrite -order_dvd; apply: dvdn_trans eGn; exact: dvdn_exponent.
apply big_prop=> [|p q|x Gx]; [exact: dvd1n | exact: dvdn_lcm | ].
by rewrite order_dvd eGn.
Qed.

Lemma trivg_exponent : forall G, trivg G = (exponent G %| 1).
Proof.
move=> G.
by apply/subsetP/exponentP=> trG x; move/trG; rewrite expg1; move/set1P.
Qed.

Lemma exponent1 : exponent (1 : {set gT}) = 1%N.
Proof. by apply/eqP; rewrite -dvdn1 -trivg_exponent trivg1. Qed.

Lemma exponent_dvdn : forall G, exponent G %| #|G|.
Proof.
by move=> G; apply/exponentP=> s Gx; apply/eqP; rewrite -order_dvd order_dvd_g.
Qed.

Lemma ltn_0exponent : forall G, 0 < exponent G.
Proof. move=> G; exact: ltn_0dvd (exponent_dvdn G). Qed.
Hint Resolve ltn_0exponent.

Lemma pnat_exponent : forall pi G, pi.-nat (exponent G) = pi.-group G.
Proof.
move=> pi G; congr (_ && _); first by rewrite ltn_0group ltn_0exponent.
apply: eq_all_r=> p; rewrite !mem_primes ltn_0group ltn_0exponent /=.
case pr_p: (prime p) => //=; apply/idP/idP=> pG.
  exact: dvdn_trans pG (exponent_dvdn G).
case/Cauchy: pG => // x Gx <-; exact: dvdn_exponent.
Qed.

Lemma abelemP : forall E,
  reflect (exists2 p, prime p & p.-abelem E) (abelem E).
Proof.
rewrite /p_abelem => E; apply: (iffP idP) => [abE | [p _]]; last by case/andP.
case: (andP abE) => _; move/pgroup_p; case/p_groupP => p pr_p pE.
by rewrite abE; exists p.
Qed.

Lemma p_abelemE : forall p E,
  prime p -> p.-abelem E = abelian E && (exponent E %| p).
Proof.
move=> p E pr_p; rewrite /p_abelem -andbA; congr (_ && _).
have e_E := exponent_dvdn E; apply/andP/idP => [[eE pE] | pE].
  case/p_natP: (pnat_dvd e_E pE) => // [[|k]] def_e; first by rewrite def_e.
  suff: (exponent E).-nat p by rewrite pnatE //; move/eqnP->.
  by apply: pnat_dvd eE; apply: dvdn_trans e_E; rewrite def_e dvdn_mulr.
have pgE: p.-group E by rewrite -pnat_exponent; exact: pnat_dvd (pnat_id _).
split=> //; case/primeP: pr_p => _ pr_p; move/pr_p: pE.
rewrite orbC; case/predU1P=> [-> // |].
by rewrite -dvdn1 -trivg_exponent; move/trivGP->; exact: pgroup1.
Qed.

Lemma p_abelemP : forall p E, prime p ->
  reflect (abelian E /\ forall x, x \in E -> x ^+ p = 1) (p.-abelem E).
Proof.
move=> p E pr_p; rewrite p_abelemE //.
by apply: (iffP andP) => [] [->]; move/exponentP.
Qed.

Lemma abelem_pgroup : forall p E, p.-abelem E -> p.-group E.
Proof. by move=> p E; case/andP. Qed.

Lemma abelem_Ohm1P : forall E,
  abelian E -> reflect ('Ohm_1(E) = E) (abelem E).
Proof.
move=> E abelE; apply: (iffP (abelemP E)) => [[p pr_p pAE] |].
  have pE: p.-group E by case/andP: pAE.
  apply/eqP; rewrite eqset_sub Ohm_subset (OhmE 1 pE) sub_gen //.
  apply/subsetP=> x Ex; rewrite inE Ex expn1 /=.
  by case/p_abelemP: pAE => // _ ->.
case pE: (p_group E); last first.
  by rewrite /Ohm pgroup_functor_1 ?pE // => E1; rewrite -E1 p_group1 in pE.
case/p_groupP: pE => p pr_p pE; rewrite (OhmE 1 pE).
rewrite gen_set_id => [defE |]; last first.
  apply/group_setP; split=> [|x y]; rewrite !inE ?(exp1gn, group1) //=.
  case/andP=> Ex; move/eqP=> xp; case/andP=> Ey; move/eqP=> yp.
  rewrite groupM // expMgn ?(xp, yp, mulg1) //=; exact: (centsP abelE).
exists p => //; apply/p_abelemP=> //; split=> // x.
by rewrite -defE inE expn1; case/andP=> _; move/eqP.
Qed.

Lemma pnElemE : forall p n G,
  prime p -> 'E_p^n(G) = [set E \in 'E_p(G) | #|E| == (p ^ n)%N].
Proof.
move=> p n G pr_p; apply/setP=> E; rewrite 3!inE -2!andbA; congr (_ && _).
case pE: (p.-abelem E) => //=; case/andP: pE => _; case/p_natP=> // k ->.
by move/pfactorK: pr_p => pK; rewrite pK (can_eq pK).
Qed.

Lemma Elem1P : forall G E,
  reflect (E \subset G /\ exists2 p, prime p & #|E| = p) (E \in 'E^1(G)).
Proof.
move=> G E; rewrite ['E^1(G)]big_mkord.
apply: (iffP (bigcupP _ _ _)) => [[[/= p _] _ pE]|[sEG [p pr_p pE]]].
  have pr_p: prime p by move: pE; rewrite 2!inE andbC lognE; case prime.
  move: pE; rewrite pnElemE // 2!inE expn1 -andbA; case/and3P=> sEG _.
  by move/eqP; split; last exists p.
have lt_p_G1: p < #|G|.+1 by rewrite ltnS dvdn_leq // -pE group_dvdn.
exists (Ordinal lt_p_G1); rewrite //= pnElemE // 2!inE sEG /= expn1 pE eqxx.
rewrite p_abelemE // -pE exponent_dvdn !andbT.
case: (@Cauchy _ p E) => // [|x Ex oxp]; first by rewrite pE.
suff <-: <[x]> = E by apply/centsP; apply: commute_cycle_com.
by apply/eqP; rewrite eqset_sub_card pE [#|_|]oxp cycle_h /=.
Qed.

End OhmProps.

